class Bucket {
    field Point position;

    constructor Bucket new(Point point) {
        let position = point;
        return this;
    }

    method void draw() {
        var int x, y;
        let x = position.getX();
        let y = position.getY();
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, x + 60, y + 40);
        return;
    }

    method void clear() {
        var int x, y;
        let x = position.getX();
        let y = position.getY();
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x + 60, y + 40);
        return;
    }

    method void moveLeft() {
        var int x;
        do clear();
        let x = position.getX();
        if (x > 0) {
            if (x > 10) {
                do position.setX(x - 10);
            }
            else {
                do position.setX(0);
            }
        }
        do draw();
        return;
    }

    method Point getPosition() {
        return position;
    }

    method void moveRight() {
        var int x;
        do clear();
        let x = position.getX();
        if (x < 451) {
            if (x < 441) {
                do position.setX(x + 10);
            }
            else {
                do position.setX(451);
            }
        }
        do draw();
        return;
    }

    method void dispose() {
        do position.dispose();
        do Memory.deAlloc(this);
        return;
    }
}